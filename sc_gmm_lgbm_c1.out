Mplus VERSION 8.8
MUTHEN & MUTHEN
10/31/2025   9:43 PM

INPUT INSTRUCTIONS

  TITLE: SOGMM for Self-Control only (unconditional), 1-class, complex design

  DATA: FILE = recoded_only_sc_pa_cov.dat;

  VARIABLE:
    NAMES =
       pttype2 nh2 bovwt1 covwt1 dovwt1 eovwt1 fovwt1 govwt1 sptn00 sc3thac
       sc3tcom sc3obey sc3dist sc3temp sc3rest sc3fidg sc5thac sc5tcom sc5obey
       sc5dist sc5temp sc5rest sc5fidg sc5lyin sc7thac sc7tcom sc7obey sc7dist
       sc7temp sc7rest sc7fidg sc7lyin sc11thac sc11tcom sc11obey sc11dist
       sc11temp sc11rest sc11fidg sc11lyin sc14thac sc14tcom sc14obey sc14dist
       sc14temp sc14rest sc14fidg sc14lyin sc17thac sc17tcom sc17obey sc17dist
       sc17temp sc17rest sc17fidg sc17lyin ignore3 smack3 shout3 bedroom3
       treats3 telloff3 bribe3 ignore5 smack5 shout5 bedroom5 treats5 telloff5
       bribe5 reason5 rindoor5 dinner5 close5 read5 story5 music5 paint5
       active5 games5 park5 ignore7 smack7 shout7 bedroom7 treats7 telloff7
       bribe7 reason7 rindoor7 dinner7 close7 read7 story7 music7 paint7
       active7 games7 park7 tvrules7 bedroom11 treats11 reason11 close11
       active11 games11 pwhere14 pwho14 pwhat14 cmwhere14 cmwho14 cmwhat14
       cmwhere17 cmtback17 pwhere17 ptback17 livewp17 pedu bpedu sex race
       brace bmarried incomec incomel incomef lbw namhapna0 namunfaa0 nambrusa0
       namfeeda0 naminjua0 nambatha0 namwarya0 nambshya0 namfreta0 namsleea0
       nammilka0 namsltia0 namnapsa0 namsofoa0 inftempr inftemp dfpw dupd
       dupw hfae coga scoga age3 age5 age7 age11 age14 age17 mcsid;

    USEVARIABLES =
      sc3thac sc3tcom sc3obey sc3dist sc3temp sc3rest sc3fidg
      sc5thac sc5tcom sc5obey sc5dist sc5temp sc5rest sc5fidg
      sc7thac sc7tcom sc7obey sc7dist sc7temp sc7rest sc7fidg
      sc11thac sc11tcom sc11obey sc11dist sc11temp sc11rest sc11fidg
      sc14thac sc14tcom sc14obey sc14dist sc14temp sc14rest sc14fidg
      sc17thac sc17tcom sc17obey sc17dist sc17temp sc17rest sc17fidg
  	GOVWT1 MCSID PTTYPE2 SPTN00;

    CATEGORICAL =
  	sc3thac sc3tcom sc3obey sc3dist sc3temp sc3rest sc3fidg
      sc5thac sc5tcom sc5obey sc5dist sc5temp sc5rest sc5fidg
      sc7thac sc7tcom sc7obey sc7dist sc7temp sc7rest sc7fidg
      sc11thac sc11tcom sc11obey sc11dist sc11temp sc11rest sc11fidg
      sc14thac sc14tcom sc14obey sc14dist sc14temp sc14rest sc14fidg
      sc17thac sc17tcom sc17obey sc17dist sc17temp sc17rest sc17fidg;

    MISSING = ALL (-9999);
    IDVARIABLE = mcsid;

    STRATIFICATION = pttype2;
    CLUSTER       = sptn00;
    WEIGHT        = govwt1;
    SUBPOPULATION = (govwt1 > 0);

    CLASSES = c(1);

  ANALYSIS:
    TYPE = MIXTURE COMPLEX;
    ESTIMATOR = MLR;
    LINK = PROBIT;
    STARTS = 300 80;
    STITERATIONS = 20;
    ! K-1STARTS = 200 40;             ! for TECH11/14 stability
    ! LRTSTARTS = 0 0 500 100;        ! bootstrap starts for TECH14
    PROCESSORS = 11(STARTS);
    ALGORITHM = INTEGRATION;
    INTEGRATION = MONTECARLO(300);

  MODEL:
    %OVERALL%
      ! (a) Measurement and invariance (identical to Step 0)
   	SC_3  BY sc3thac@1
              sc3tcom (l2)
  			sc3obey (l3)
  			sc3dist (l4)
  			sc3temp (l5)
  			sc3rest (l6)
  			sc3fidg (l7);
      SC_5  BY sc5thac@1
              sc5tcom (l2)
  			sc5obey (l3)
  			sc5dist (l4)
  			sc5temp (l5)
  			sc5rest (l6)
  			sc5fidg (l7);
    	SC_7  BY sc7thac@1
              sc7tcom (l2)
  			sc7obey (l3)
  			sc7dist (l4)
  			sc7temp (l5)
  			sc7rest (l6)
  			sc7fidg (l7);
    	SC_11 BY sc11thac@1
              sc11tcom (l2)
  			sc11obey (l3)
  			sc11dist (l4)
  			sc11temp (l5)
  			sc11rest (l6)
  			sc11fidg (l7);
    	SC_14 BY sc14thac@1
              sc14tcom (l2)
  			sc14obey (l3)
  			sc14dist (l4)
  			sc14temp (l5)
  			sc14rest (l6)
  			sc14fidg (l7);
    	SC_17 BY sc17thac@1
              sc17tcom (l2)
  			sc17obey (l3)
  			sc17dist (l4)
  			sc17temp (l5)
  			sc17rest (l6)
  			sc17fidg (l7);


      SC_3*; SC_5*; SC_7*; SC_11*; SC_14*; SC_17*;
      [SC_3-SC_17@0];

      [sc3thac$1](t_thac1); [sc5thac$1](t_thac1); [sc7thac$1](t_thac1);
  	[sc11thac$1](t_thac1); [sc14thac$1](t_thac1); [sc17thac$1](t_thac1);
      [sc3thac$2](t_thac2); [sc5thac$2](t_thac2); [sc7thac$2](t_thac2);
  	[sc11thac$2](t_thac2); [sc14thac$2](t_thac2); [sc17thac$2](t_thac2);

      [sc3tcom$1](t_tcom1); [sc5tcom$1](t_tcom1); [sc7tcom$1](t_tcom1);
  	[sc11tcom$1](t_tcom1); [sc14tcom$1](t_tcom1); [sc17tcom$1](t_tcom1);
      [sc3tcom$2](t_tcom2); [sc5tcom$2](t_tcom2); [sc7tcom$2](t_tcom2);
  	[sc11tcom$2](t_tcom2); [sc14tcom$2](t_tcom2); [sc17tcom$2](t_tcom2);

      [sc3obey$1](t_obey1); [sc5obey$1](t_obey1); [sc7obey$1](t_obey1);
  	[sc11obey$1](t_obey1); [sc14obey$1](t_obey1); [sc17obey$1](t_obey1);
      [sc3obey$2](t_obey2); [sc5obey$2](t_obey2); [sc7obey$2](t_obey2);
  	[sc11obey$2](t_obey2); [sc14obey$2](t_obey2); [sc17obey$2](t_obey2);

      [sc3dist$1](t_dist1); [sc5dist$1](t_dist1); [sc7dist$1](t_dist1);
  	[sc11dist$1](t_dist1); [sc14dist$1](t_dist1); [sc17dist$1](t_dist1);
      [sc3dist$2](t_dist2); [sc5dist$2](t_dist2); [sc7dist$2](t_dist2);
  	[sc11dist$2](t_dist2); [sc14dist$2](t_dist2); [sc17dist$2](t_dist2);

      [sc3temp$1](t_temp1); [sc5temp$1](t_temp1); [sc7temp$1](t_temp1);
  	[sc11temp$1](t_temp1); [sc14temp$1](t_temp1); [sc17temp$1](t_temp1);
      [sc3temp$2](t_temp2); [sc5temp$2](t_temp2); [sc7temp$2](t_temp2);
  	[sc11temp$2](t_temp2); [sc14temp$2](t_temp2); [sc17temp$2](t_temp2);

      [sc3rest$1](t_rest1); [sc5rest$1](t_rest1); [sc7rest$1](t_rest1);
  	[sc11rest$1](t_rest1); [sc14rest$1](t_rest1); [sc17rest$1](t_rest1);
      [sc3rest$2](t_rest2); [sc5rest$2](t_rest2); [sc7rest$2](t_rest2);
  	[sc11rest$2](t_rest2); [sc14rest$2](t_rest2); [sc17rest$2](t_rest2);

      [sc3fidg$1](t_fidg1); [sc5fidg$1](t_fidg1); [sc7fidg$1](t_fidg1);
  	[sc11fidg$1](t_fidg1); [sc14fidg$1](t_fidg1); [sc17fidg$1](t_fidg1);
      [sc3fidg$2](t_fidg2); [sc5fidg$2](t_fidg2); [sc7fidg$2](t_fidg2);
  	[sc11fidg$2](t_fidg2); [sc14fidg$2](t_fidg2); [sc17fidg$2](t_fidg2);

      i s | SC_3@0 SC_5* SC_7* SC_11* SC_14* SC_17@1;
  	[i s];      ! Estimate intercept and slope means
      i*;
  	s*;      ! Estimate intercept and slope variances
      i WITH s;   ! Estimate intercept-slope covariance

  OUTPUT:
    TECH1 TECH4 SVALUES;
    SAVEDATA: FILE = sc_sogmm_cprob_1c.dat; SAVE = CPROBABILITIES;



*** WARNING
  Data set contains cases with missing on all variables.
  These cases were not included in the analysis.
  Number of cases with missing on all variables:  2050
   1 WARNING(S) FOUND IN THE INPUT INSTRUCTIONS



SOGMM for Self-Control only (unconditional), 1-class, complex design

